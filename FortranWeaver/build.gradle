plugins {
    id 'distribution'
}

// Java project
apply plugin: 'java'

// Executable
apply plugin: 'application'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}


// Repositories providers
repositories {
    mavenCentral()
}

configurations {
    weaverGeneratorRuntime
}

dependencies {

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation ":jOptions"
    implementation ":SpecsUtils"
    implementation ":GsonPlus"

    implementation ":LanguageSpecification"
    implementation ":LARAI"
    implementation ":LaraUtils"
    implementation ":WeaverInterface"

    implementation ":FortranAst"
    implementation ":FortranParser"


    //implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.1'

    weaverGeneratorRuntime ":WeaverGenerator"
}

java {
    withSourcesJar()
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }

        resources {
            srcDir 'resources'
        }
    }


    test {
        java {
            srcDir 'test'
        }

        resources {
            srcDir 'resources-test'
        }
    }

}

test {
    useJUnitPlatform() // REQUIRED for JUnit 5
}


// Run the weaver generator
task generateWeaver(type: JavaExec) {
    group = "Execution"
    description = "Generates the Weaver Abstracts"
    classpath = configurations.weaverGeneratorRuntime
    mainClass = 'org.lara.interpreter.weaver.generator.commandline.WeaverGenerator'
    args = [
            "-w", "FortranWeaver",
            "-x", "./resources/fortran/weaverspecs",
            "-o", "./src",
            "-p", "pt.up.fe.specs.fortran.weaver",
            "-n", "pt.up.fe.specs.fortran.ast.nodes.FortranNode",
            "-j",
            "-d",
    ]
}

compileJava.dependsOn generateWeaver